@PACKAGE_INIT@

set(CARIBOU_TARGETS @Caribou_COMPONENTS@)

if (Caribou_FIND_COMPONENTS)
    foreach(component ${Caribou_FIND_COMPONENTS})
        if (NOT ";${CARIBOU_TARGETS};" MATCHES ${component})
            set(Caribou_FOUND False)
            set(Caribou_NOT_FOUND_MESSAGE "Unsupported component: ${component}. Available components are ${CARIBOU_TARGETS}")
        else()
            # For requested component, execute its "config" script
            set_and_check(config_file ${CMAKE_CURRENT_LIST_DIR}/${component}Config.cmake)
            include(${config_file})
            set(Caribou_${component}_FOUND True)
        endif()
    endforeach()
else()
    foreach(component ${CARIBOU_TARGETS})
        # For all available components, execute its "config" script
            set_and_check(config_file ${CMAKE_CURRENT_LIST_DIR}/${component}Config.cmake)
            include(${config_file})
            set(Caribou_${component}_FOUND True)
    endforeach()
endif()

# OPTIONS
set(CARIBOU_USE_DOUBLE "@CARIBOU_USE_DOUBLE@")
set(CARIBOU_BUILD_TESTS "@CARIBOU_BUILD_TESTS@")
set(CARIBOU_WITH_SOFA "@CARIBOU_WITH_SOFA@")
set(CARIBOU_WITH_PYTHON_2 "@CARIBOU_WITH_PYTHON_2@")
set(CARIBOU_WITH_PYTHON_3 "@CARIBOU_WITH_PYTHON_3@")

include("${CMAKE_CURRENT_LIST_DIR}/CaribouTargets.cmake")

check_required_components(Caribou)
